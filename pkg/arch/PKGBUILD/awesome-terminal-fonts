# Author: Loi Tran <tdloi at protonmail[dot]com>
# Modified from official awesome-terminal-fonts PKGBUILD

_pkgname=awesome-terminal-fonts
pkgname=awesome-terminal-fonts-forked
pkgver=1.2.0
pkgrel=1
pkgdesc="awesome terminal fonts/icons for powerlines (removed fontawesome + pomicons)"
arch=('any')
url="https://github.com/tdloi/awesome-terminal-fonts"
license=('MIT')
depends=('fontconfig')
conflicts=('awesome-terminal-fonts')
source=("${_pkgname}-${pkgver}.tar.gz::$url/archive/v${pkgver}.tar.gz")
sha512sums=('122a9f6101f88f1c27d2e27263aa85b2184b5b41521c6ae4553407defa69c28db58f4768c62732c968f4512bd13dc989853ccfaffd0408f26b5e39fe04325aa0')


package() {
  install -d "${pkgdir}/usr/share/fonts/${_pkgname}"
  install -d "${pkgdir}/etc/fonts/conf.avail"
  install -Dm 0644 "${srcdir}/${_pkgname}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE"
  install -Dm 0644 "${srcdir}/${_pkgname}-${pkgver}/README.md" "${pkgdir}/usr/share/doc/${_pkgname}/README.md"
  cd "${srcdir}/${_pkgname}-${pkgver}/build"
  install -m 0644 * "${pkgdir}/usr/share/fonts/${_pkgname}/"
  install -m 0644 "${srcdir}/${_pkgname}-${pkgver}/config/10-symbols.conf" "${pkgdir}/etc/fonts/conf.avail"
  mkdir -p "${pkgdir}/etc/fonts/conf.d"
  ln -s "/etc/fonts/conf.avail/10-symbols.conf" "${pkgdir}/etc/fonts/conf.d/10-symbols.conf"
}
